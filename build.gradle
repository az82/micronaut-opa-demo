buildscript {
    repositories {
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "com.github.jengelman.gradle.plugins:shadow:2.0.4"
        classpath "io.spring.gradle:dependency-management-plugin:1.0.5.RELEASE"
        classpath "net.ltgt.gradle:gradle-apt-plugin:0.15"
        classpath "gradle.plugin.com.google.cloud.tools:jib-gradle-plugin:0.9.3"
        classpath "gradle.plugin.com.github.lkishalmi.gatling:gradle-gatling-plugin:0.7.3"
    }
}


// Version = GIT commit
version 'git rev-parse HEAD'.execute().text
group "de.az.demo.mn.opa"


apply plugin: "io.spring.dependency-management"
apply plugin: "com.github.johnrengelman.shadow"
apply plugin: "application"
apply plugin: "groovy"
apply plugin: "net.ltgt.apt-eclipse"
apply plugin: "net.ltgt.apt-idea"
apply plugin: "com.google.cloud.tools.jib"
apply plugin: "com.github.lkishalmi.gatling"


repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
}

dependencyManagement {
    imports {
        mavenBom 'io.micronaut:bom:1.0.0.BUILD-SNAPSHOT'
    }
}

dependencyManagement {
    gatling {
        dependencies {
            dependencySet(group: 'io.netty', version: '4.0.51.Final') {
                entry 'netty-codec-http'
                entry 'netty-codec'
                entry 'netty-handler'
                entry 'netty-buffer'
                entry 'netty-transport'
                entry 'netty-common'
                entry 'netty-transport-native-epoll'
            }
        }
    }
}
dependencies {
    annotationProcessor "io.micronaut:inject-java"
    compile "io.micronaut:http-client"
    compile "io.micronaut:http-server-netty"
    compile "io.micronaut:inject"
    compile "io.micronaut:runtime"
    compile "io.micronaut:management"
    compileOnly "io.micronaut:inject-java"
    runtime "ch.qos.logback:logback-classic:1.2.3"
    testCompile "io.micronaut:inject-groovy"
    testCompile("org.spockframework:spock-core:1.1-groovy-2.4") {
        exclude group: "org.codehaus.groovy", module: "groovy-all"
    }
    testCompile "com.github.tomakehurst:wiremock-standalone:2.18.0"
}

shadowJar {
    mergeServiceFiles()
}

jib {
    to {
        auth {
            // Credential helper for docker does not seem to work properly
            // Provide credentials using gradle.properties or environment variables
            username = findProperty('jibToAuthUsername') ?: System.getenv('JIB_TO_AUTH_USERNAME')
            password = findProperty('jibToAuthPassword') ?: System.getenv('JIB_TO_AUTH_PASSWORD')
        }
    }
    container {
        jvmFlags = ['-Xms32m', '-Xmx32m']
        ports = ['8080']
    }
}

run.jvmArgs('-noverify', '-XX:TieredStopAtLevel=1')

mainClassName = "de.az.demo.mn.opa.Application"
compileJava.options.compilerArgs += '-parameters'
compileTestJava.options.compilerArgs += '-parameters'
